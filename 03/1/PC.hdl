// This file is part of the materials accompanying the book 
// "The Elements of Computing Systems" by Nisan and Schocken, 
// MIT Press. Book site: www.idc.ac.il/tecs
// File name: projects/03/1/PC.hdl

/**
 * 16-bit counter with load and reset controls.
 *
 * If reset(t-1) then out(t) = 0
 *    else if load(t-1) then out(t) = in(t-1)
 *         else if inc(t-1) then out(t) = out(t-1) + 1 (integer addition)
 *              else out(t) = out(t-1)
 */

CHIP PC {

    IN  in[16], load, inc, reset;
    OUT out[16];

    PARTS:
    Inc16(in=regout, out=incd);
    Mux16(a=regout, b=incd, sel=inc, out=incmux);
    Mux16(a=incmux, b=in, sel=load, out=loadmux);
    Mux16(a=loadmux, b=false, sel=reset, out=resmux);

    Register(in=resmux, load=true, out=regout, out=out);
}
